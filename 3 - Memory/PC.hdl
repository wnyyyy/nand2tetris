// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    //should update
    Or(a=reset, b=load, out=resetorload);
    Or(a=resetorload, b=inc, out=shouldupdate);
    Register(in=resetout, load=shouldupdate, out=register, out=out);
    //should increment
    Inc16(in=register, out=incouta);
    Mux16(a=register, b=incouta, sel=inc, out=incoutb);
    //should load
    Mux16(a=incoutb, b=in, sel=load, out=loadout);
    //should reset
    Mux16(a=loadout, b=false, sel=reset, out=resetout);
}